# @desc Template to illustrate the parameters and flags

# Define a template to package the shutter in-out-in from 6.40
# with flags to enable or disable in-out or out-in
template animation shutter_bidir {
  param colors type palette
  param period default 2s
  param inout type bool default true    # define to true to enable 'inout' part
  param outin type bool default true   # define to true to enable 'outin' part

  # since 'strip_length()' is a value provider, it must be assigned to a variable before being used
  set strip_len = strip_length()

  # Define animated value for the size of the shutter, evolving linearly in time (sawtooth from 0% to 100%)
  set shutter_size = sawtooth(min_value = 0, max_value = strip_len, duration = period)

  # Define two rotating palettes, shifted by one color
  color col1 = color_cycle(colors=colors, period=0)
  color col2 = color_cycle(colors=colors, period=0)
  col2.next = 1     # move 'col2' to the next color so it's shifte by one compared to 'col1'

  # Shutter moving in in-out
  animation shutter_inout_animation = beacon_animation(
    color = col2
    back_color = col1
    pos = 0
    beacon_size = shutter_size
    slew_size = 0
    priority = 5
  )

  # shutter moving in out-in
  animation shutter_outin_animation = beacon_animation(
    color = col1
    back_color = col2
    pos = 0
    beacon_size = strip_len - shutter_size
    slew_size = 0
    priority = 5
  )

  # this is the overall sequence composed of two sub-sequences
  # the first in ascending mode, the second in descending
  sequence shutter_seq repeat forever {
    if inout {                              # un only if 'ascending' is true
      repeat col1.palette_size times {          # run the shutter animation
        restart shutter_size                    # resync all times for this animation, to avoid temporal drift
        play shutter_inout_animation for period    # run the animation
        col1.next = 1                           # then move to next color for both palettes
        col2.next = 1
      }
    }
    if outin {                             # run only if 'descending' is true
      repeat col1.palette_size times {
        restart shutter_size
        play shutter_outin_animation for period
        col1.next = 1
        col2.next = 1
      }
    }
  }
  run shutter_seq
}

# define a palette of rainbow colors including white with constant brightness
palette rainbow_with_white = [
  0xFC0000        # Red
  0xFF8000        # Orange
  0xFFFF00        # Yellow
  0x00FF00        # Green
  0x00FFFF        # Cyan
  0x0080FF        # Blue
  0x8000FF        # Violet
  0xCCCCCC        # White
]

animation main = shutter_bidir(colors = rainbow_with_white, period = 1.5s)
run main
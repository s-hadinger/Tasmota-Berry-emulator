# @desc Shutter left to right with rotating colors, using sequence

# Define 'strip_len' to make calculations
set strip_len = strip_length()

# Set a global variable for 'period'
set period = 1.5s

# Define shutter_size as a sawtootgh from 0 to strip_len
set shutter_size = sawtooth(min_value = 0, max_value = strip_len,
                            duration = period)

# Define 2 color providers cycling through palette rainbow with white
# 'col2' is shifted by 1 color from 'col1'
color col1 = color_cycle(colors=PALETTE_RAINBOW_W, period=0)
color col2 = color_cycle(colors=PALETTE_RAINBOW_W, period=0)
col2.next = 1           # Writing 1 to 'next' actually advances the color

# Using beacon_animation to move a shutter from left to right
animation shutter_lr_animation = beacon_animation(
    color = col2                    # Use two rotating colors
    back_color = col1               # Use two rotating colors
    pos = 0                         # Start from position 0 (left)
    beacon_size = shutter_size     
     # Oscillating size from 0 to strip_len
)

# Define a sequence running forever
sequence shutter_seq repeat forever {
    restart shutter_size        # Make sure that shutter_size is synced with the sequence
    play shutter_lr_animation for period    # Play animation
    col1.next = 1               # set 'col1' to next color
    col2.next = 1               # set 'col2' to next color
}
run shutter_seq